plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.appdistribution'
}

def baseVersion = "1.31.0"
android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    buildFeatures {
        viewBinding = true
    }

    defaultConfig {
        applicationId "co.proxy.sdk.example"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug
        multiDexEnabled true
    }

    flavorDimensions "env", "market"

    productFlavors {
        staging {
            applicationId "co.proxy.sdk.example.staging"
            dimension "env"
            firebaseAppDistribution {
                serviceCredentialsFile = "$rootDir/app/src/staging/proxy-sdk-example-staging-credentials.json"
                releaseNotesFile = "./app/release-notes.txt"
                groups = "android---beta"
            }
        }
        production {
            applicationId "co.proxy.sdk.example"
            dimension "env"
            firebaseAppDistribution {
                serviceCredentialsFile = "$rootDir/app/proxy-sdk-example-production-credentials.json"
                releaseNotesFile = "./app/release-notes.txt"
                groups = "android---beta"
            }
        }
        world {
            dimension "market"
        }
        china {
            dimension "market"
            versionName "$baseVersion-cn"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
        }
    }

    compileOptions {
        incremental true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    dexOptions {
        preDexLibraries true
    }
    lintOptions {
        checkReleaseBuilds false
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "com.google.android.material:material:$materialdesign_version"

    //Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    //ViewModel+LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.legacy:legacy-support-v4:$legacy_support_version"

    //Navigation
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"

    //Injection
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    //ProxySDK
    worldImplementation 'co.proxy:android-sdk:1.9.2'
    chinaImplementation 'co.proxy:android-sdk-cn:1.9.2'

    //Images
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    //Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //Animations
    implementation "com.airbnb.android:lottie:$lottie_version"

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0-alpha03'
    androidTestImplementation 'com.schibsted.spain:barista:3.2.0'
    testImplementation "androidx.arch.core:core-testing:$arch_testing_version"
    testImplementation('org.assertj:assertj-core:3.16.1')
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.6.2')
    testImplementation('io.mockk:mockk:1.10.0')
}

hilt {
    enableExperimentalClasspathAggregation = true
}